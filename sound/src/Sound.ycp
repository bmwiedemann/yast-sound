/**
 * File:
 *   modules/Sound.ycp
 *
 * Package:
 *   Configuration of sound
 *
 * Summary:
 *   Input and output functions.
 *
 * Authors:
 *   Dan Meszaros <dmeszar@suse.cz>
 *
 * $Id$
 *
 * Representation of the configuration of sound.
 * Input and output routines.
 *
 */

{
    // Set the name of the module
    module "Sound";

    include "sound/read_routines.ycp";
    include "sound/alsa_routines.ycp";
    include "sound/routines.ycp";
    include "misc_defines.ycp";

    include "browser.ycp";

    // list for storing detected cards
    global list detected_cards = nil;

    // list of detected and unconfigured cards
    global list unconfigured_cards = nil;

    // settings to save to /etc/modules.conf (only those concerning to sound)
    global list modules_conf = [];

    // what sound system we're using (true=alsa, false=oss)
    global boolean use_alsa = true;

    // settings to save using .audio.alsa ... mixer
    global list volume_settings = [];

    // rc settings
    global map rc_vars = $[];

    // alsa sound card database
    global map db_cards = nil;
    global map db_modules = nil;
    global map db_indices = nil;
    global map db_module_index = nil;

    /**
     * do hardware detection
     */

    global define DetectHardware() ``{
	// do noop if cards were already detected
	if (detected_cards != nil)
	{
	    return true;
	}
	detected_cards  = SCR::Read(.probe.sound);
	return true;
    }


    /**
     * searches for sound alias in /etc/modules.conf
     */

    global define ReadModulesConf() ``{
	modules_conf = read_save_info();
    }

    /**
     * update list of unconfigured cards (necessaty when deleting configured card)
     */

    global define UpdateUnconfiguredCards() ``{
	unconfigured_cards = getConfigurableCards(modules_conf);
    }

    /**
     * Read all sound settings from the SCR
     * @return boolean True on success
     */
    global define Read(block _unused) ``{
	// first detect sound cards
	DetectHardware();
	// load data from /etc/modules.conf
	ReadModulesConf();
	// create list of unconfigured cards
	UpdateUnconfiguredCards();
	// read rc.config values
	rc_vars = read_rc_vars (["START_ALSA", "START_ALSA_SEQ"]);
	// load volume settings
	volume_settings = get_vol_settings();
	return true;
    }

    /**
     * Get all sound settings from the first parameter
     * (For use by autoinstallation.)
     * @return boolean True on success
     */
    global define Import(map settings) ``{
	// initialize these unneeded values
	detected_cards = [];
	unconfigured_cards = [];

	// import values
	if (lookup(settings, "modules_conf", nil) == nil)
	{
	    y2error("Import error: 'modules_conf' key is missing");
	    return false;
	}
	modules_conf = lookup(settings, "modules_conf", []);

	rc_vars = lookup(settings, "rc_vars", $[]);

	volume_settings = lookup(settings, "volume_settings", []);

	return true;
    }

    /**
     * Update the SCR according to sound settings
     * @return boolean True on success
     */
    global define Write(block _unused, block _unused2) ``{
	CallFunction (`sound_write(modules_conf, rc_vars));
	return true;
    }

    /**
     * Dump the sound settings to a single map
     * (For use by autoinstallation.)
     * @return map Dumped settings (later acceptable by Import())
     */
    global define Export() ``{
	return $[
		"modules_conf"	    : modules_conf,
		"rc_vars"	    : rc_vars,
		"volume_settings"   : volume_settings
	    ];
    }

    /**
     * opens alsa sound cards database
     */

    global define LoadDatabase() ``{
	map sound_db =$[];
	if (db_cards == nil || db_cards == $[])
	{
	    textdomain "sound_db";
	    y2debug("Reading card database");
	    sound_db = eval(SCR::Read (.target.yast2, "sndcards.ycp"));

	    db_cards	= lookup(sound_db, "cards", $[]);
	    db_modules	= lookup(sound_db, "modules", $[]);
	    db_indices	= lookup(sound_db, "indices", $[]);
	    db_module_index = lookup(sound_db, "mod_idx", $[]);

	    textdomain "sound";
	}
    }

    /**
     * just writes all variables to log. don't use in Master.
     */

    global define DumpToLog() ``{
	y2error(sformat("%1", [Sound::modules_conf, rc_vars, true, true, get_vol_settings()]));
    }

}
