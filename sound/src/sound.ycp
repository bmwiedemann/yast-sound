/**
 *
 *
 * File:
 *   sound.ycp
 *
 * Module:
 *   Sound 	 
 *
 * Authors:
 *   Dan Meszaros <dmeszar@suse.cz>
 *
 * $Id$
 *
 * Installation of the sound card. If the sound card was not auto-detected ask user.
 *		     
 *
 */

{

    textdomain "sound";

    include "misc_defines.ycp";

    include "wizard/sequencer.ycp";
    include "ui/wizard_hw.ycp";
    include "ui/common_popups.ycp";
    include "ui/wizard_dialog.ycp";

//    include "sound/alsa_routines.ycp";
    include "sound/card_wizard.ycp";
    include "sound/read_routines.ycp";
    include "sound/routines.ycp";
    include "sound/ui.ycp";
    include "sound/write_routines.ycp";

    boolean __sparc=nil;
    boolean use_alsa = !is_sparc();
    if (size(Args()) > 0 && Args(0)==.oss)
    {
	use_alsa=false;
    }	

    if (use_alsa)
    {
	include "sound/alsa_routines.ycp";
    }
    else
    {
	include "sound/oss_routines.ycp";
    }
    include "sound/texts.ycp";

    // text constants for sound module
    map STRINGS=STRINGS_();
    float oldf=0.0; 

    // just cover functions for wizard sequencer
    // these global variables are used and modified: 
    //	  save_info
    //	  card_id
    //    sound_cards  

    /*
     * card selection
     *
     */


    map save_entry = $[];
    boolean was_complex = false;

    global define _snd_select() ``{
	sound_cards = getConfigurableCards(save_info);
	if ((size(sound_cards) == 0) && (!was_complex))
	{
	    return `detail;
	}
	list nh	    = maplist(`e, sound_cards, ``(lookup(e, "model", "")));

        list ch	    = maplist(`e, save_info, ``(lookup(e, "model", ""))); 
	map res	    = CallFunction(`sound_select(nh, save_info));
	symbol ui   = lookup(res, "ui", `abort);

	card_id = lookup(res, "id", 0);
	if (card_id == -1)
	{
	    return `manual;
	}
	return lookup(res, "ui", `abort);
    }

    /**
     *
     *
     */

    global define _snd_quick() ``{
	save_entry = select(sound_cards, 0);
	card_id = 0;
	string modelname    = lookup(save_entry, "model", "");
	map res		    = quickConfig(modelname, "snd-card-0", card_id, 15);
	symbol ui	    = lookup(res, "ui", nil);
	return ui;	
    }

    global define _snd_quick2(boolean type) ``{
	save_entry  = add_common_options(save_entry, 0);
	save_entry  = add_alias(save_entry, 0);
	map res	    = CallFunction (`sound_volume(save_entry, 0, true, type, []));
	symbol ui   = lookup(res, "ui", nil);
	if (ui == `next)
	{
	    save_info = [save_entry];
	}
	return ui;
    }

    /*
     * card selection
     *
     */

    global define _snd_manual() ``{
	map res = CallFunction(`sound_manual());

	// if user selects the soundcards that has been already autodetected, 
	// use the detected card instead of manual selection (because it causes
	// some problems with uniq. keys)

	symbol ui = lookup(res, "ui", `abort);
    
	boolean override = false;
	string uniq_k = "";
	string label = "";


	if (ui == `next)
	{
	    uniq_k = isa_uniq();
	    label = lookup(res, "model", "Sound card");

	    list cds = filter(`e, sound_cards, ``(lookup(e, "module", "") == lookup(res, "module", nil)));
	    y2debug("%1", cds);
	    if (size(cds) > 0)
	    {
		label  = lookup(select(cds, 0), "model", "Sound card");
		uniq_k = lookup(select(cds, 0), "unique_key", isa_uniq());
	    }

	    save_entry = $[
		"module"     : lookup(res, "module", ""),
		"model"	     : label,
		"unique_key" : uniq_k
		];
	    card_id = -1;
	}	
	if (ui == `back)
	{
	    save_entry = $[];
	}
	return ui;
    }

    /*
     * card selection
     *
     */

    global define _snd_add() ``{
	list nh     = maplist(`e, sound_cards, ``(lookup(e, "model", "")));
	map res	    = WhichDialog(nh);
	symbol ui   = lookup(res, "ui", `back);
        card_id	    = lookup(res, "card_id", -1);
        if (card_id == -1 && ui == `next)
        {
            return `manual;
        }

	return ui;	
    }

    /** 
     * sound card deletion
     *
     */

    global define _snd_delete() ``{
	if (UI::YesNoPopup(_("Do you really want to delete this entry?")))
	{
	    boolean terminate = stop_programs();
	    if (terminate == false)
            {
                return `next;
            }
            string modname = lookup(select(save_info, card_id), "module", "");

            // we have to rember volume/mute settings because after a card removal
            // everything is muted and set to 0.
            list vol_settings = get_vol_settings();

            list rem	    = get_indices_for_removal(save_info, modname, card_id);
            save_info	    = remove_save_entries(save_info, rem);
            vol_settings    = multi_remove(vol_settings, rem);

            SaveModulesEntry (save_info, [], true);

	    sound_stop();
            sound_start_tmp(true);
            y2debug(sformat("%1", vol_settings));
            set_vol_settings(vol_settings);
 
	    // now we have to update the unconfigured cards list
	    sound_cards = getConfigurableCards(save_info); 

	}

	return `next;
    }

    /*
     * card selection
     *
     */

    global define _snd_config( /* symbol finish_sym */) ``{
	if (card_id >= 0)
	{
	    save_entry = select(sound_cards, card_id);
	}
	map res = OneCardWizard(save_entry, size(save_info), false, 15, false, save_info);

        symbol ui = lookup(res, "ui", nil);
        if (ui == `next)
        {
            save_info = add(save_info, lookup(res, "return", $[]));
	    if (card_id >= 0)
	    {
		sound_cards = remove(sound_cards, card_id);
	    }
            // TODO: another dialog should be called
	    if (size(sound_cards) == 0)
	    {
		return `detail;
	    }
        }
	return ui;
    }

    /*
     * card selection
     *
     */

    global define _snd_complex() ``{
	was_complex = true;
	map res	    = CallFunction(`sound_complex(save_info));
        symbol ui   = lookup(res, "ui", nil);
        if (ui == `next)
        {
            save_info	= lookup(res, "save_info", []);
            rc_vars	= lookup(res, "rc_vars", []);
            state	= `save;
        }
	return ui;
    }

    /*
     *
     * ========== MAIN ==========
     *
     */
//if (!use_alsa)
//{
//    UI::MessagePopup("BETA INFO for OSS:\nThis module requires to have 'mpg123' and 'aumix'
//installed on your system. Also copy some (short) mp3 file to
///usr/share/sounds/test.mp3 . This file will be played as
//test sound.");
//}

    OpenMainDialog();
    InitializingDialog ();

    UI::DisableWizardAbortButton();
    boolean test_mode     = false;
    boolean config_mode   = false;
    boolean autoinst_mode = false;
//    boolean use_alsa      = true;	// default
    boolean from_menu     = false;

    integer arg_n = size (Args ()) - 1;
    while (arg_n >= 0) 
    {
	if (Args(arg_n) == .test)
	{ 
	    test_mode     = true;
	}
	if (Args(arg_n) == .autoinst_mode) 
	{
	    autoinst_mode = true;
	}
	if (Args(arg_n) == .conf_mode)    
	{
	    config_mode   = true;
	}
	if (Args(arg_n) == .from_menu)
	{
	    from_menu     = true;
	}
	arg_n = arg_n - 1;
    }
    list sound_cards  = []; 
    list save_info    = read_save_info ();


    map rc_vars       = $[];
    integer card_id   = 0;
    integer n_cards   = 0;
    map result        = $[];
//    any ui            = nil;
    boolean cont      = true;

    list configured_unique = []; // list of unique keys, that was configured

    boolean exit = false;
    list sound_db = nil;

    if (use_alsa && SCR::Read(.target.size, "/usr/lib/YaST2/sndcards.ycp")==-1) 
    {
	UI::MessagePopup(_("Sound card database not found. Please check your installation."));
	UI::CloseDialog();
	return;			
    }
    
    sound_cards = getConfigurableCards(save_info);

    integer all_cards_num = size(sound_cards);

    if (exit)
    {	// no sound cards database present, shutting down
	UI::CloseDialog ();
	return;
    }
    SCR::Execute(.target.bash, "/bin/rm /tmp/modules.conf > /dev/null 2>&1; /bin/touch /tmp/modules.conf > /dev/null 2>&1", $[]);
    
    nm256_opl3sa2_warn(sound_cards);

    symbol state = `card_select;

    
// test data
//    save_info=[];
//    sound_cards=[$["model":"Pokusna karta", "module": "snd-card-emu10k1", "unique_key":"kx", "options": []]];

    string initial = "intro";

    if (size(sound_cards) == 1 && size(save_info) == 0)
    {
	card_id = 0;
	initial = "quick";
    }
    else if (size(sound_cards) == 0)
    {
	initial = "complex";
    }
    

    map m_aliases = $[
	    "intro"	: ``(_snd_select()),

	    "config1"	: ``(_snd_config()),
	    "config2"	: ``(_snd_config()),

	    "manual1"	: ``(_snd_manual()),
	    "manual2"	: ``(_snd_manual()),

	    "complex"	: ``(_snd_complex()),
	    "delete"	: ``(_snd_delete()),
	    "select"	: ``(_snd_add()),

	    // quick configuration
	    "quick"	: ``(_snd_quick()),
	    "quick2"	: ``(_snd_quick2(true)),
	    "quick3"	: ``(_snd_quick2(false))
	];

    map m_sequence = $[
	    "ws_start"	: initial,
	    "quick"	:
		$[
		    `quick	    :	"quick2",
		    `normal	    :	"quick3",
		    `abort	    :	`abort,
		    `intro	    :	"intro"
		    
		],
	    "quick2"	:
		$[
		    `next	    :	`finish,
		    `abort	    :	`abort
		],
	    "quick3"	:
		$[
		    `next	    :	`finish,
		    `abort	    :	`abort
		],


	    "intro"	: 
		$[
		    `next	    :	`finish,
		    `configure	    :   "config1",
		    `manual	    :	"manual1",
		    `detail	    :	"complex",
		    `abort	    :	`abort
		],


	    "config1"	: 
		$[
		    `next	    :	"intro",
		    `detail	    :	"intro",
		    `abort	    :   `abort
		],
	    "manual1"	: 
		$[
		    `next	    :	"config1",
		    `abort	    :	`abort		
		],

	    "config2"	: 
		$[
		    `next	    :	"complex",
		    `detail	    :	"complex",
		    `abort	    :   `abort
		],
	    "manual2"	: 
		$[
		    `next	    :	"config2",
		    `abort	    :	`abort		
		],
	    

	    "complex"	:
		$[
		    `next	    :	`finish,
		    `abort	    :	`abort,
		    `add_manual     :	"manual2",
		    `add_select	    :	"select",
		    `delete	    :	"delete"
		],
	    "select"	:
		$[
		    `abort	    :	`abort,
		    `next	    :	"config2",
		    `manual	    :	"manual2"
		],
	    "delete"	:
		$[
		    `next	    :	"complex"
		]
	];

    symbol ui = WizardSequencer(m_aliases, m_sequence);

    if (ui == `back || ui == `abort)
    {
	UI::CloseDialog();
	return;
    }

			    // filter out empty enries
    save_info = filter (`e, save_info, ``(size (e) > 0));
    n_cards = size (save_info);

    map save_map = $[];

    if (ui == `finish) 
    {
	CallFunction (`sound_write(save_info, rc_vars));

    // always save unique keys 
	save_unique_keys (maplist(`e, save_info, ``(lookup(e, "unique_key", ""))));
    }

    UI::CloseDialog ();
}
