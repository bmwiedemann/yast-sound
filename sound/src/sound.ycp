/**
 *
 *
 * File:
 *   sound.ycp
 *
 * Module:
 *   Sound 	 
 *
 * Authors:
 *   Dan Veselý	<dan@suse.cz>
 *
 * $Id$
 *
 * Installation of the sound card. If the sound card was not auto-detected ask user.
 *		     
 *
 */

{

    textdomain "sound";

    include "misc_defines.ycp";
    include "sound/routines.ycp";
    include "sound/ui.ycp";
    include "sound/texts.ycp";

    // text constants for sound module
    map STRINGS=STRINGS_();
  
    // Menuentry for the YaST2 menu
    if (Args() == [ "get_menuentry" ])
    {
	return [ "sound", $[
	    `menuentry    : UI(_("Hardware/Sound")),
	    `arguments    : [ ],
	    `widget       : `RichText(
	    //
	    UI(_("Launch this module to configure your sound card."))
	    +
	    UI(_("<p>You need to be logged in as <i>root</i> in order to do this.</p>"))),
	    `codefragment : nil ]
	];
    }

    
    /**
     * Wizard steps: 1. displayname 
     * <br>
     * 2. modprobe and volume settings
     * @param   card_info as returned by libhd
     * @return  list with save info, as required by sound_write for one cards
     */
    define OneCardWizard (map card, integer card_id, any ui) ``{
	list dialog = [``(DisplayName (hardware_name (card), sformat("snd-card-%1", card_id))),
		    ``(CallModule ("sound_volume", [card, card_id, (size (sound_cards) == 1)] ))];

	integer id = 0;
	if (ui == `back)
	{
	    id = size (dialog) - 1;
	}
	map result = $[];
	any ui = nil;

	boolean success = false;
	any act_dialog = nil;

	while ((id >= 0) && (id < size (dialog))) 
	{

	    act_dialog = select (dialog, id);

	    if (id+1 == size (dialog))
	    {
		if (!nm256hack(card)) 
		{
		    id = 0;
		    continue;
		}
	    }    
	    result = eval(act_dialog);
	    ui = lookup(result, "ui", `cancel);
	    
	    if ((ui == `cancel) || (ui == `skip))
	    {
		break;
	    }
	    
	      // case: next
	    else if (ui == `next) 
	    {
		id = id + 1;

		if (id == size (dialog)) 
		{
		    success = lookup (result, "return", false);
		}
		else 
		{
		    if (act_dialog == ``(CallModule ("sound_options", [card]))) 
		    {
			card = lookup (result, "return");
		    }
		}
	    }
		// case: back
	    else if (ui == `back) 
	    {
		id = id - 1;
		if (id == 0) 
		{
		    dialog = filter (`e, dialog, ``(e != ``(CallModule ("sound_options", [card]))));
		}
	    }
		// case: options
	    else if (ui == `options) 
	    {
		list nd   = [];
		integer i = 0;
		// insert into the pre-last position
		foreach (`e, dialog, ``{
		    if ((size (dialog) - 1) == i) 
		    {
			nd = add (nd, ``(CallModule ("sound_options", [card])));
		    }
		    nd = add (nd, select(dialog, i));
		    i = i + 1;
		});
		dialog = nd;
		id = id + 1;
	    }
	}

	map ret = $[];

	if (((ui == `next) && success) || (ui == `back)) 
	{
	    result = create_save_info (card, card_id);
	    if (size (result) > 0)
	    {
		ret = add (ret, "return", result);
	    }
	}

	if (no_alsa_exception (card, config_mode))
	{
	    ret = add (ret, "use_alsa", false);
	}
	return add (ret, "ui", ui);
    }

    // ========== MAIN ==========

    OpenMainDialog();
    InitializingDialog ();

    boolean test_mode     = false;
    boolean config_mode   = false;
    boolean autoinst_mode = false;
    boolean use_alsa      = true;	// default
    boolean from_menu     = false;

    integer arg_n = size (Args ()) - 1;
    while (arg_n >= 0) 
    {
	if (Args(arg_n) == .test)
	{ 
	    test_mode     = true;
	}
	if (Args(arg_n) == .autoinst_mode) 
	{
	    autoinst_mode = true;
	}
	if (Args(arg_n) == .conf_mode)    
	{
	    config_mode   = true;
	}
	if (Args(arg_n) == .from_menu)
	{
	    from_menu     = true;
	}
	arg_n = arg_n - 1;
    }

    list sound_cards  = get_audio (test_mode, config_mode);
    list save_info    = read_save_info ();
    
    y2error(sformat("SaveInfo:%1",save_info ));

    map rc_vars       = $[];
    integer card_id   = 0;
    integer n_cards   = 0;
    map result        = $[];
    any ui            = nil;
    boolean cont      = true;
    boolean call_complex_dialog = false;	
    list configured_unique = []; // list of unique keys, that was configured

    boolean exit = false;
    list|void db = GetCardDatabase(); 
    if (is (db, void)) 
    {
	UI(`DisplayMessage ("The Sound card database cannot be found!!!"));
	exit = true;
    }

    // get all the apropriate information from the database
    sound_cards =  maplist(`e, sound_cards, ``(add (e, "module", get_module (e))));

    // all configured cards
    list|void already = nil;
    if (SCR(`Read(.target.size, "/var/lib/YaST/unique.inf")) > 0)
    {
	already = SCR (`Read(.var.lib.yast.unique));
    }

    if (!is(already, void)) 
    {
	already = maplist (`k, lookup (select (filter (`e, already, ``(lookup (e, "section") == "sound")), 0, $[]), "contents", []),
			   ``(lookup (k, "unique")));
	// filter out cards that had been already configured
	sound_cards = filter (`e, sound_cards, ``(!contains (already, lookup (e, "unique_key"))));
    }

    // TODO: delete entries from save_info that have no corresponding record in /proc/asound/cards 

    save_info=remove_invalid_save_info(save_info);

    if(exit)
    {	// no sound cards database present, shutting down
	UI(`CloseDialog ());
	return;
    }


    boolean skipped = false;
    call_complex_dialog = ( size (sound_cards) != 1 );	
    while (cont) 
    {
		    // wizards ...
	while ((card_id >= 0) && (card_id < size (sound_cards))) 
	{

	    result = OneCardWizard (select (sound_cards, card_id), size (save_info), condition (skipped, `next, ui));

	    ui       = lookup (result, "ui", `cancel);
	    use_alsa = lookup (result, "use_alsa", true);

	    if (ui == `cancel)
	    {
		break;
	    }
	    if (ui == `skip) 
	    {
		card_id = card_id + 1;
		skipped = true;
	    }
	    if (ui == `back) 
	    {
		map rem = lookup (result, "return", $[]);
		save_info = filter (`e, save_info, ``(e != rem));
		configured_unique = filter (`e, configured_unique, ``(lookup (select (sound_cards, card_id), "unique_key", "") != e));
		card_id = card_id - 1;
	    }
	    if (ui == `next) 
	    {
		map new = lookup (result, "return", $[]);
		save_info = add (save_info, new);
		configured_unique = add (configured_unique, lookup (select (sound_cards, card_id), "unique_key", ""));
		card_id = card_id + 1;
		skipped = false;
	    }
    
	}

	if (ui == `back) 
	{   // exit module
	    break;
	}
    
	if (call_complex_dialog) 
	{
	    result = CallModule ("sound_complex", [sound_cards, save_info, configured_unique]);
	}
	else
	{
	    result = $["ui" : `next, "save_info" : save_info];
	}

	ui = lookup (result, "ui", `cancel);

	if (ui == `next && (size (sound_cards) != 1)) 
	{
	
      // confirmation popup message
	    string warn_msg = lookup(STRINGS, "ConfigSaveWarn");

	    if (size(filter (`e, lookup (result, "save_info"), ``((size (e) > 0) && (lookup (e, "name") != "off")))) == 0)
	    {
	// confirmation popup message		
		warn_msg = lookup(STRINGS, "ConfigSaveWarn2");
	    }

	// Yes, No - button label
	    cont = !(UI (`YesOrNo (warn_msg, _("&Yes"), _("&No"))));
	
	    if (call_complex_dialog) 
	    {
		save_info = lookup (result, "save_info");
		rc_vars   = lookup (result, "rc", $[]);
		configured_unique = lookup (result, "configured_unique", []);
	    }
	    else
	    {
		skipped = true;
	    }
	}
	else 
	{
	    if ((size (sound_cards) != 1))
	    {
		cont = false;
	    }
	    else 
	    {
		if (ui == `back) 
		{
		    card_id = card_id - 1;
		}
	    }

	}
	if ((size (sound_cards) == 1)) 
	{
	    cont = false;
	}
    }

			    // filter out empty enries
    save_info = filter (`e, save_info, ``(size (e) > 0));
    n_cards = size (save_info);

    map save_map = $[];

    if ((n_cards > 0) && (ui == `next)) 
    {
	if (use_alsa) 
	{
	    string snd_name = "off";

	    if (size (filter (`e, save_info, ``(lookup (e, "name") != "off"))) > 0)
	    {
		snd_name = "snd";
	    }

	    save_info = prepend (save_info, $["name"   : snd_name,
				      "modules" : $["alias"  : "char-major-116",
						    "options" : $["snd_cards_limit" : sformat ("%1", n_cards),
						     "snd_major"        : "116"]]]);

	    save_map = add (save_map, "cards",  save_info);
	    save_map = add (save_map, "system", $["modules" : alsa_oss (n_cards),
					     "rc"       : rc_vars]);
	    save_map = add (save_map, "other",  ``(AlsaFinish ()));

	}
	else 
	{
	// no alsa
	    string mod_name = lookup (select (sound_cards, 0), "module", "");
	    if (size (mod_name) > 0)
	    {
		save_map = add (save_map, "system", no_alsa (mod_name));
	    }
	}

    //UI(`DebugDialog (save_info));

	CallModule ("sound_write", [save_map, test_mode]);

    
    }    

    if (ui == `next) 
    {
    // always save unique keys 
	save_unique_keys (get_audio (test_mode, config_mode), configured_unique);

	if (lookup (rc_vars, "START_ALSA") == "no")
	{
	    SCR(`Execute (.bin.bash, "/etc/init.d/alsasound stop", $[]));
	}
    }

    UI(`CloseDialog ());
}
