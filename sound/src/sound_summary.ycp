/**
 *
 *
 * File:
 *   sound_summary.ycp
 *
 * Module:
 *   Sound       
 *
 * Authors:
 *   Dan Meszaros <dmeszar@suse.cz>
 *
 * $Id$
 *
 * Soud installation summary. returns list of already configured and not configured sound cards
 *             
 * @param skipNotConfigured if set to true, list of notconfigured cards will be excluded      
 *
 */

{
    textdomain "sound";

    include "ui/summary.ycp";
    include "misc_defines.ycp";
    include "sound/routines.ycp";
    include "sound/read_routines.ycp";

    boolean skipNotConfigured=false;

    if(Args()>=1)
    {
	skipNotConfigured=Args(0);
    }

    list cards=get_audio();
    list keys=read_unique_keys();
    list save_info=read_save_info ();

    list conflist=[];	// configured
    list notlist=[];	// not configured
    list retlist=[];	// return value of the module
    // copy cards: configured are added to the front of list, unconfigured to the end
	
    string key="";
    
    foreach(`entry, save_info, 
    ``{
	conflist=add(conflist, lookup(entry, "model", ""));
    });

    foreach(`card, cards, 
    ``{
	key=lookup(card, "unique_key", "");
	if(!contains(keys, key))
        {
            notlist=add(notlist, get_card_label(card));
        }
    });

    integer i=0;

    foreach(`card, conflist, 
    ``{
	// to translators: '(sound card is) Configured as snd-card-1'
	retlist=add(retlist, summaryDevice(card, sformat(_("Configured as snd-card-%1."), i)));
	i=i+1;
    });

    if(!skipNotConfigured)
    {  
	foreach(`card, notlist,
	``{
	    retlist=add(retlist, summaryDevice(card, summaryNotConfigured()));
	});
    }

    return summaryDevicesList(retlist);

}
