/**
 * File:
 *   sound_manual.ycp
 * 
 * Module:
 *   Sound
 * 
 * Summary:
 *   Module where an attemp of inserting module is provided,
 *   if everything goes well adjusting volume is done here
 *   otherwise error message is displayed
 * 
 * Authors:
 *   Dan Veselý <dan@suse.cz>
 *
 * $Id$
 *
 *
 * parameters: 1st: module, Map, map of card
 *             2nd: card_id, integer
 *             3rd: boolean - finish
 *
 */

{

    textdomain "sound";
	
    include "misc_defines.ycp";
    include "sound/routines.ycp";
    include "sound/ui.ycp";
    include "sound/texts.ycp";
    include "sound/write_routines.ycp";


    map STRINGS=STRINGS_();

    /**
     * This dialog sipmply reads user input and calls apropriate set funtion
     *
     * @return symbol
     */

    define VolumeDialog () ``{

	    //unmute these devices
	/* help text: adjusting volume */

	string help_text=lookup(STRINGS, "VolumeDialog");

	integer vol = 10;

	term con=VolumeCon(save_entry, vol);
	    // dialog title
	UI(`SetWizardContents (_("Sound card volume"), con, help_text, true, true));
	if (finish)
	{
	    UI(`ChangeWidget (`id(`next), `Label, FinishButtonLabel()));
	}

/* Sound fonts installation */
	if(HasFonts(save_entry) && !FontsInstalled())
	{
	    InstallFonts("", false);
	}

/* end of sound fonts installation */

	

	SetVolume ("Master", card_id, vol);

	repeat 
	{
	    ui =  UI (`UserInput ());

	    if (ui == `volume) 
	    {
		vol = UI(`QueryWidget(`id(`volume), `Value ));
		SetVolume ("Master", card_id, vol);
	    }
	    else if (ui == `test) 
	    {
		if (!test_mode)
		{
		    string msg=PlayTest(user_settings, card_id);
		    if(msg != "")
		    {
			UI (`MessagePopup (msg));
		    }
		}
	    }
	    else if (ui == `next) 
	    {
		if (finish)
		{
		    if (!UI (`YesNoPopup (_("Sound volume and the configuration\nfor the sound card will be saved now."))))
		    {
		      continue;
		    }
		}
		ui = `next;
	    }
	    else if (ui == `abort || ui == `cancel)
	    {
		if(ReallyAbort())
		{
		    ui=`cancel;
		    break;
		}
	    }
		      
	} until ((ui == `back) || (ui == `next) || ui == `cancel);
	return ui;
    }

    /**
    * shows error message in wizard
    * @return symbol `back | `cancel
    */

    define ErrorDialog (map save_entry, string err) ``{
				    // Help text - intenal YaST2 error 

	string help_text=lookup(STRINGS, "ErrorDialog");
	    
	term con = `HVCenter
	  (
	    `VBox
	    (
		    // error message
	      `Label (_("An error occured during installing")),
	      `VSpacing (),
	      `Label (`opt(`outputField), lookup(save_entry, "model", "")),
	      `VSpacing (),
	      `Label (err)
	    )
	  );
	    
	    // dialog title
	UI(`SetWizardContents (ErrorMsg(), con, help_text, true, false));
	if (finish)
	{
	    UI(`ChangeWidget (`id(`next), `Label, FinishButtonLabel()));
	}

	repeat 
	{
	    ui =  UI (`UserInput ());
            if(ui == `cancel || ui == `abort)
            {
                if(ReallyAbort()) 
                {
                    return `abort;
                }
            }

	} until ((ui == `back) || ui == `cancel);

	return ui;
    }

    /**
    * shows success message in wizard
    * @return symbol `back | `cancel
    */

    define SuccessDialog (map save_entry) ``{
                                    // Help text - intenal YaST2 error 

        string help_text=lookup(STRINGS, "SuccessDialog");

        term con = `HVCenter
          (
            `VBox
            (
                    // error message
              `Label (_("Your sound card is ready to use.")),
              `VSpacing (),
              `Label (`opt(`outputField), lookup(save_entry, "model", "")),
              `VSpacing (),
              `Label ("")
            )
          );

            // dialog title
        UI(`SetWizardContents (_("Success"), con, help_text, true, true));
        if (finish)
        {
            UI(`ChangeWidget (`id(`next), `Label, FinishButtonLabel()));
        }

        repeat
        {
            ui =  UI (`UserInput ());
	    if(ui == `cancel || ui == `abort)
	    {
		if(ReallyAbort()) 
		{   
		    return `abort;
		}
	    }
        } until ((ui == `back) || ui == `next);

        return ui;
    }




  // ===== main =====
      /*
       * Steps:
       *        1. try to insert kernel module
       *        2. on succes unmute and volume dialog
       *        3. on fail display error message
       */
    boolean need_restart=true;
    map save_entry = Args (0);
    integer card_id = Args (1);
    boolean finish = false;

    boolean quick = false;

    UI(`DisableWizardAbortButton());

    if (size (Args ()) > 2)
    {
	finish = Args (2);
    }
    
    if (size (Args ()) >3)
    {
	quick= Args (3);
    }
    
    list save_info=[];
    if (size (Args())>4)
    {
	save_info=Args(4);
    }

    map user_settings = $["audio_file"  : "/usr/share/sounds/alsa/test.wav"];

//			  "play_cmd"    : "/usr/bin/aplay",
//			  "mixer"       : "/usr/bin/amixer -c $CARD_NUM set \"$CHANNEL\" $VOLUME% unmute"];
      
    string err_msg = "";

    stop_programs();    

    if (!test_mode)
    {
	SCR(`Execute(.target.bash, "/etc/init.d/alsasound stop", $[]));
	list new_save=add(save_info, save_entry);
	SaveModulesEntry (new_save, [], true);
	alsasound_start_tmp(true);
	err_msg = check_module(save_entry);
    }
    any ui = nil;

    if (size (err_msg) > 0)
    {
	ui = ErrorDialog (save_entry, err_msg);
    }
    else
    {
        maplist (`dev, ["Master", "PCM", "PCM,1", "Wave", "Master Digital", "Synth"],
    		    ``(SetVolume (dev, card_id, 70)));
	unmute(["Master", "PCM", "PCM,1", "Wave", "Master Digital", "Synth"], card_id);
	if(!quick)
	{
	    ui = VolumeDialog ();
	}
	else
	{
	    SetVolume ("Master", card_id, 75);
	    ui = SuccessDialog(save_entry);
	}
    }

    if (ui == `back || ui == `cancel || ui == `abort)
    {
	SaveModulesEntry(save_info, [], true);
	alsasound_start_tmp(true);	
    }


    UI(`ChangeWidget (`id(`next), `Label, NextButtonLabel()));

    return $["ui" : ui, "return" : size (err_msg) == 0];

}


