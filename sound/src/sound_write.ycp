/**
 * File:
 *   sound_write.ycp
 *
 * Module:
 *   Sound
 *
 * Summary:
 *   Provides saving /etc/modules.conf and volume.
 *
 * Authors:
 *   Dan Veselý <dan@suse.cz>
 *   Dan Meszaros <dmeszar@suse.cz>
 *
 * $Id$
 *
 * Params:
 *         1st ... save info- list of configured cards:
 *	   [
 *		$[
 *		    "model"	: "sb live!",
 *		    "alias"	: "snd-card-0",
 *		    "options"	: $["snd_id":"0" ...],
 *		    "module"	: "snd-emu10k1",
 *		    "unique_key": "abcd.efghijklmn"
 *		],
 *              $[
 *                  "model"     : "avi onboard!",
 *                  "alias"     : "snd-card-1",
 *                  "options"   : $["snd_id":"1" ...],
 *                  "module"    : "snd-asf",
 *                  "unique_key": "abcd.abcdefghij"
 *              ]
 *         ]
 *
 *	   2nd ... sysconfig values
 *	   $[
 *		"LOAD_SEQUENCER" : "yes"
 *         ]
 *
 *	   3rd ... use_alsa - boolean
 *
 *	   4th ... autoinstall mode boolean
 *
 *	   5th ... volume setting (usefull only for autoconfig)
 *	   [
 *		[  // card #1
 *		    ["PCM", 32, false], ["Master", 100, true] ...
 *	        ],
 *		[  // card #2
 *		    
 *		],
 *		...
 *
 *	   ]
 *
 * Steps:
 *        1. save '/etc/modules.conf'
 *        2. save '/etc/rc.config'
 *        3. save volume
 *        4. call function to provide some additional work
 *
 * Return boolean true on success, false if failed
 */

{


    textdomain "sound";


    include "sound/write_routines.ycp";
    include "sound/alsa_routines.ycp";

    import  "Progress";
    import  "Wizard";
    import  "Runlevel";

    import "Sound";
    import "Joystick";

      // ==== MAIN ====

    boolean test_mode	= false;
    map rc_values		= $[];

    string helptext	= "";
    string err_msg	= "";

    list save_info	= [];
    boolean alsa	= Sound::use_alsa;
    boolean autoinstall = false;

    map settings = WFM::Args(0);
    save_info = lookup(settings, "modules_conf", []);
    rc_values = lookup(settings, "rc_vars", []);
    volume_settings = lookup(settings, "volume_settings", []);
    autoinstall = lookup(settings, "autoinstall", false);

    // do nothing when proposal is empty (Sound::installation is set to true in proposal mode)
    if (Sound::installation && size(save_info) == 0)
    {
	y2debug("empty proposal. exiting.");
	return true;
    }
    if (autoinstall)
    {
//	UI::Wizard::CreateDialog();
    }

    list stones = [
	    // progress bar item
	    _("Save module configuration"),
	    // progress bar item
            _("Save sound card information"),
	    // progress bar item
	    _("Save sysconfig values"),
	    // progress bar item
	    _("Start sound card"),
	    // progress bar item
	    _("Store volume"),
	    // progress bar item
	    _("Store joystick settings")];

    list stones2 = [
	    // progress bar item
	    _("Saving sound card settings."),
	    // progress bar item
	    _("Saving card information..."),
	    // progress bar item
	    _("Saving sysconfig values..."),
	    // progress bar item
	    _("Starting sound card..."),
	    // progress bar item
	    _("Storing volume settings"),
	    // progress bar item
	    _("Storing joystick settings...")];


    if (!autoinstall)
    {
	// progres bar label
	Progress::New (_("Saving sound card settings."), " ", size(stones) - 1,
		// progres bar label
		stones, stones2, _("Saving sound card settings."));
        Wizard::DisableAbortButton();
    }

    integer step = 0;

    if (!autoinstall) { Progress::NextStage(); }

    step = step + 1;
    SaveModulesEntry(save_info, []);

    if (!autoinstall) { sleep(100); Progress::NextStage(); }
    step = step + 1;
    if (!autoinstall)
    {
	Sound::StoreUniqueKeys();
    }

    if (!autoinstall) { sleep(100); Progress::NextStage(); }
    step = step + 1;
    SaveRCValues(rc_values);

    if (!autoinstall) { sleep(100); Progress::NextStage(); }
    step = step + 1;

    list configuredcards = Sound::GetSoundCardList();

    if (!autoinstall)
    {
	// stop joystick before restarting ALSA
	SCR::Execute(.target.bash, "/etc/init.d/joystick stop", $[]);

	// restart ALSA
	if (size(configuredcards) > 0)
	{
	    SCR::Execute(.target.bash, "/etc/init.d/alsasound start", nil);
	}
    }

    if (!autoinstall) { Progress::NextStage(); }
    if (size(WFM::Args()) >= 4)
    {
	set_vol_settings(volume_settings);
    }
    if (SCR::Read(.target.size, "/etc/asound.state") == -1)
    {
	integer i = 0;
	maplist(`e, save_info, ``{
	    Sound::InitMixer(i, lookup(e, "module", ""));
	    i = i + 1;
	});
    }

    SaveVolume();
    if (!autoinstall) sleep(100);

    if (!autoinstall) { sleep(100); Progress::NextStage(); }

    // write joystick configuration and restart service
    Joystick::Write(``{ return false;});
    // TODO: why is it restarted? It was done in Write, wasn't it?
    if (Joystick::joystick != [])
        SCR::Execute(.target.bash, "/etc/init.d/joystick restart", $[]);

    if (size(configuredcards) > 0)
    {
	// enable alsasound service in runlevels 2,3,5
	Runlevel::ServiceFinetune("alsasound", ["2", "3", "5"]);
    }
    else
    {
	// disable sound service - it's not needed, no soundcard is present
	Runlevel::ServiceAdjust("alsasound", "disable");
    }

    return (size (err_msg) == 0);
}
