/**
 *
 * $Id$
 *
 * File:
 *   write_routines.ycp
 *
 * Module:
 *   Sound
 *
 * Summary:
 *   API for writings sound configuration
 *
 * Authors:
 *   Dan Veselý <dan@suse.cz>
 *   Dan Meszaros <dmeszar@suse.cz>
 *
 *
 */

{
    textdomain "sound";

    import "Arch";
    import "Sound";
    include "sound/routines.ycp";

    /**
     * create special comment for given model
     * @param model card description
     * @param uniq_key unique key
     * @return string comment for the alias
     */

    define string createAliasComment(string model, string uniq_key) ``{
	string comment = "# " + uniq_key + ":" + model + "\n";
	return comment;
    }

    /**
     * Saves one '/etc/modules.conf' entry
     * @param entry card config
     * @return boolean success/failure
     */
    define boolean SaveOneModulesEntry (map entry) ``{
        path mod_alias		= add (.modules.alias, entry ["alias"]:"off");
        path mod_alias_comment	=
            add ( add (.modules.alias, entry ["alias"]:"off"), "comment");

        boolean ret = true;
        if (haskey (entry, "alias"))
        {
            ret = ret && SCR::Write(mod_alias, entry["module"]:"off");
	    string comment = createAliasComment( entry ["model"]: "",
		entry["unique_key"]:"");
	    ret = ret && SCR::Write(mod_alias_comment, comment);
        }
        return ret;
    }

    /**
     * saves modules options. this function has to collect parameters that use
     * different cards that use single module and create a coma separated list of values
     * @param save_info save info
     * @return void
     */

    define void SaveModulesOptions(list save_info) ``{

	path mod_options = .modules.options;

	// create distinct list of all modules
	list mods = toset(maplist(map card, save_info,
	    ``(card["module"]:"off")));

	foreach(string modname, mods, ``{
	    map res = Sound::CollectOptions(modname);
	    SCR::Write( add (mod_options, modname ), res);
	});
    }

    /**
     * removeOldAliases
     * @param als list with old aliases
     *
     */

    define void removeOldEntries(list als) ``{
	maplist(string e, als, ``{
	    if (is_snd_alias(e) ||
		issubstring(e, "sound-service-") ||
		issubstring(e, "sound-slot-"))
		{
		    SCR::Write( add (.modules.alias, e), nil);
		}
	});
	// seting off-values due to kdm log (bug #19602)
	integer i=0;
	while (i<4)
	{
	    SCR::Write(add (.modules.alias, sformat ("sound-slot-%1", i)),
		"off");
	    SCR::Write(add (.modules.alias, sformat ("sound-service-%1-0", i)),
		"off");
	    i = i + 1;
	}
    }

    /**
     * Saves all '/etc/modules.conf' entries
     * @param save_info cards save info
     * @param system sytem dependent part
     * @return map return struct: $["return": boolean, "err_msg": string]
     */
    define map SaveModulesEntry (list save_info, list system) ``{
	path mod		= .modules;
	path mod_alias		= .modules.alias;
	path mod_options	= .modules.options;
	SaveModulesOptions(save_info);

	list als=[];

        string err_msg = "";
	if (contains(SCR::Dir(mod), "alias"))
	{
	    als = (list)SCR::Read(mod_alias);
	}
	// remove old entries

	removeOldEntries(als);

	SCR::Write(add (mod_alias, "char-major-14"), "off");
	SCR::Write(add (mod_alias,"char-major-116"), "off");

	if (size(save_info) >0)
	{
	    if (Sound::use_alsa)
	    {
		SCR::Write(add (mod_alias, "char-major-116"), "snd");
		SCR::Write(add (add (mod_alias, "char-major-116"), "comment"), "\n# Alsa sound support. \n# Warning: please don't modify comments over " +
		    "aliases 'snd-card-#'");
		string cards_limit = Sound::snd_prefix() + "cards_limit";
		string major = Sound::snd_prefix() + "major";
		SCR::Write (add (mod_options, "snd"),
		    $[ cards_limit: sformat ("%1", size(save_info)),
		       major:"116"]);
		SCR::Write(add (mod_alias, "char-major-14"), "off");
	    }
	    else
	    {
		if (Arch::sparc)
		{
		    string modname = save_info[0, "module"]:"off";
		    SCR::Write(add (mod_alias, "sound"), "sound-slot-0");
		}
		SCR::Write( add (mod_alias, "char-major-14"), "sound");
	    }
	}

        if ((contains (maplist (map entry, save_info,
		``(SaveOneModulesEntry (entry))), false)))
        {
            // Error message
            err_msg = err_msg + Sound::STRINGS["SaveModuleEntry"]:"";
            return $["return": false, "err_msg": err_msg];
        }
	if (size(save_info) > 0 && Sound::use_alsa)
	{

	    list oss_aliases = alsa_oss(size(save_info));
	    foreach(map a, oss_aliases, ``{
		SCR::Write(add (mod_alias, a["alias"]:""), a["module"]:"");
	    });
	    SCR::Write(add (add (mod_alias, "sound-slot-0"), "comment"),
            "\n\n\n# YaST2: sound system dependent part\n#\n");
	}
	SCR::Write(mod, nil);
        return $["return" : true, "err_msg" : ""];
    }

    /**
     * Save volume (for alsa it's alsactl store
     * @return boolean success/failure
     */
    define boolean SaveVolume () ``{
	return (boolean) SCR::Execute(.audio.alsa.store, 0, 0);
    }

    /**
     * saves rc values that are stored in map
     * @param rc map "variable" : "value", ....
     * @return string error string
     */
    define string SaveRCValues (map rc) ``{
	SCR::Write (.sysconfig.sound.LOAD_SEQUENCER, rc["LOAD_ALSA_SEQ"]:"no");
	return "";
    }


}
