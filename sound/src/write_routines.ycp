/**
 *
 * $Id$
 *
 * File:
 *   write_routines.ycp
 *
 * Module:
 *   Sound
 *
 * Summary:
 *   API for writings sound configuration
 *
 * Authors:
 *   Dan Veselý <dan@suse.cz>
 *   Dan Meszaros <dmeszar@suse.cz>
 *
 *
 */

{
    textdomain "sound";

    import "Arch";
    import "Sound";
    include "sound/routines.ycp";

    /**
     * create special comment for given model
     * @param model card description
     * @param uniq_key unique key
     * @return string comment for the alias
     */

    define string createAliasComment(string model, string uniq_key) ``{
	string comment = "# " + uniq_key + ":" + model + "\n";
	return comment;
    }

    /**
     * Saves one '/etc/modules.conf' entry
     * @param entry card config
     * @return boolean success/failure
     */
    define boolean SaveOneModulesEntry (map entry) ``{

        path mod_alias	= add (.modprobe_sound.alias, entry ["alias"]:"off");
        path mod_alias_comment	=
            add ( add (.modprobe_sound.alias, entry ["alias"]:"off"),"comment");

        boolean ret = true;
        if (haskey (entry, "alias"))
        {
            ret = ret && SCR::Write(mod_alias, entry["module"]:"off");
	    string comment = createAliasComment( entry ["model"]: "",
		entry["unique_key"]:"");
	    ret = ret && SCR::Write(mod_alias_comment, comment);
        }
        return ret;
    }

    /**
     * saves modules options. this function has to collect parameters that use
     * different cards that use single module and create a coma separated list of values
     * @param save_info save info
     * @return void
     */

    define void SaveModulesOptions(list save_info) ``{

	path mod_options = .modprobe_sound.options;

	// create distinct list of all modules
	list mods = toset(maplist(map card, (list<map<string,any> >) save_info,
	    ``(card["module"]:"off")));

	foreach(string modname, (list<string>) mods, ``{
	    map res = Sound::CollectOptions(modname);
	    SCR::Write( add (mod_options, modname ), res);
	});
    }

    /**
     * removeOldAliases
     * @param als list with old aliases
     */
    define void removeOldEntries(list als) ``{
	maplist(string e, (list<string>) als, ``{
	    if (is_snd_alias(e) ||
		issubstring(e, "sound-service-") ||
		issubstring(e, "sound-slot-"))
		{
		    SCR::Write( add (.modprobe_sound.alias, e), nil);
		}
	});
    }

    /**
     * Remove sound configuration from /etc/modprobe.conf
     */
    define void RemoveOldConfiguration () ``{

	y2milestone ("removing old sound configuration from /etc/modprobe.conf (it was already saved to /etc/modprobe.d/sound)");

	path mod		= .modules;
	path mod_alias		= .modules.alias;
	path mod_options	= .modules.options;

	if (contains(SCR::Dir(mod), "alias"))
	{
	    list<string> als = (list<string>)SCR::Read(mod_alias);
	    foreach (string e, als, ``{
		if (is_snd_alias(e) ||
		    issubstring(e, "sound-service-") ||
		    issubstring(e, "sound-slot-") ||
		    e == "char-major-14" ||
		    e == "char-major-116")
		{
		    SCR::Write (add (mod_alias, e), nil);
		}
	    });
	}
	// TODO remove also old options
//	SCR::Write( add (mod_options, modname ), nil);
	SCR::Write (mod, nil);
    }

    /**
     * Saves all '/etc/modprobe.d/sound' entries
     * @param save_info cards save info
     * @param system sytem dependent part
     * @return map return struct: $["return": boolean, "err_msg": string]
     */
    define map SaveModulesEntry (list save_info, list system) ``{

	path mod		= .modprobe_sound;
	path mod_alias		= .modprobe_sound.alias;
	path mod_options	= .modprobe_sound.options;
	SaveModulesOptions(save_info);

	list als=[];

        string err_msg = "";
	if (contains(SCR::Dir(mod), "alias"))
	{
	    als = (list)SCR::Read(mod_alias);
	}
	// remove old entries

	removeOldEntries (als);

	if (size(save_info) >0)
	{
	    if (!Sound::use_alsa)
	    {
		if (Arch::sparc)
		{
		    string modname = save_info[0, "module"]:"off";
		    SCR::Write(add (mod_alias, "sound"), "sound-slot-0");
		}
	    }
	}

        if ((contains (maplist (map entry, (list<map<string,any> >)save_info,
		``(SaveOneModulesEntry (entry))), false)))
        {
            // Error message
            err_msg = err_msg + Sound::STRINGS["SaveModuleEntry"]:"";
            return $["return": false, "err_msg": err_msg];
        }
	/* also this is not neccessary now... (?)
	if (size(save_info) > 0 && Sound::use_alsa)
	{

	    list oss_aliases = alsa_oss(size(save_info));
	    foreach(map a, (list<map<string,any> >)oss_aliases, ``{
		SCR::Write(add (mod_alias, a["alias"]:""), a["module"]:"");
	    });
	}
	*/
	SCR::Write(mod, nil);

	// now, when new configuration was succesfully written, we should remove
	// the old one from /etc/modprobe.conf...
	if (Sound::used_modprobe_conf)
	    RemoveOldConfiguration ();

        return $["return" : true, "err_msg" : ""];
    }

    /**
     * Save volume (for alsa it's alsactl store
     * @return boolean success/failure
     */
    define boolean SaveVolume () ``{
	return (boolean) SCR::Execute(.audio.alsa.store, 0, 0);
    }

    /**
     * saves rc values that are stored in map
     * @param rc map "variable" : "value", ....
     * @return string error string
     */
    define string SaveRCValues (map rc) ``{
	SCR::Write (.sysconfig.sound.LOAD_SEQUENCER, rc["LOAD_ALSA_SEQ"]:"no");
	return "";
    }


}
